
// Apply the java-library plugin to add support for Java Library
plugins {
    id 'java'
    id 'application'
}
group 'io.vantiq'
version 'unspecified'


java {
    sourceCompatibility = 1.8
}

// In this section you declare where to find the dependencies of your project
repositories {
    mavenCentral()
}

// 
startScripts{
    doLast{
        def windowsScriptFile = file getWindowsScript()
        def unixScriptFile = file getUnixScript()
        // Add the log Config to the top of the classpath
        windowsScriptFile.text = windowsScriptFile.text.replace("CLASSPATH=", "CLASSPATH=%APP_HOME%\\logConfig;")
        unixScriptFile.text = unixScriptFile.text.replace('CLASSPATH=', 'CLASSPATH=$APP_HOME/logConfig:')
    }
}

application {
    mainClassName = 'io.vantiq.extsrc.udp.ConfigurableUDPSource'

// Copies the README and licenses into the distribution
    applicationDistribution.from(".") {
        include "README.md"
        include "LICENSE/*"
        into ""
    }

// Copies the logger setup into logConfig in the distribution
    applicationDistribution.from("src/main/resources") {
        include "log4j2.xml"
        into "logConfig"
    }
}

dependencies {
   // compile 'extjsdk' // extjsdk.jar must be in
    implementation project(":extjsdk")

    implementation "org.slf4j:slf4j-api:1.7.25"
    implementation "org.apache.logging.log4j:log4j-slf4j-impl:${log4jVersion}"

    implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.2'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.9.4'    //XML parsing
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-csv:2.9.4'
    

    // This dependency is used internally, and not exposed to consumers on their own compile classpath.
    implementation "com.google.guava:guava:${guavaVersion}"

    // Use JUnit test framework
    testImplementation 'junit:junit:4.13.1'
    testImplementation project(path:":extjsdk", configuration:"testArtifacts")
}