group 'io.vantiq'
version 'unspecified'

apply plugin: 'java'
apply plugin: 'application'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
    
    // Used for checking VANTIQ date format behavior with VANTIQ SDK
    maven {
    	url "https://dl.bintray.com/vantiq/maven"
    }
}
configurations {
    externalLibs
}
mainClassName = 'io.vantiq.extsrc.HikVisionSource.HikVisionMain'

startScripts{
    doLast{
        def windowsScriptFile = file getWindowsScript()
        def unixScriptFile = file getUnixScript()
        // Add the log Config to the top of the classpath
        windowsScriptFile.text = windowsScriptFile.text.replace("CLASSPATH=", "CLASSPATH=%APP_HOME%\\logConfig;")
        unixScriptFile.text = unixScriptFile.text.replace('CLASSPATH=', 'CLASSPATH=$APP_HOME/logConfig:')
    }
}

// Copies the logger setup into logConfig in the distribution
applicationDistribution.from("src/main/resources") {
    include "log4j2.xml"
    into "logConfig"
}

tasks.withType(Test) {

    if (rootProject.hasProperty("EntConTestSourceName")) {
        systemProperty "EntConTestSourceName", rootProject.findProperty("EntConTestSourceName")
    }
    if (rootProject.hasProperty("TestAuthToken")) {
        systemProperty "TestAuthToken", rootProject.findProperty("TestAuthToken") ?: "empty"
    }
    if (rootProject.hasProperty("TestVantiqServer")) {
        systemProperty "TestVantiqServer", rootProject.findProperty("TestVantiqServer") ?: "empty"
    }
}
if (System.env.HIKVISION_LOC == null ) {
    throw new IllegalArgumentException("Required environment variable HIKVISION_LOC not found.")
}

dependencies {
    // This dependency is used internally, and not exposed to consumers on their own compile classpath.
    implementation 'com.google.guava:guava:23.0'
    
    compile project(':extjsdk')
    
    compile "org.slf4j:slf4j-api:1.7.25"
    compile "org.apache.logging.log4j:log4j-slf4j-impl:2.11.0"
    
      
    // Used for checking VANTIQ date format behavior with VANTIQ SDK
    compile 'io.vantiq:vantiq-sdk:1.0.27'
    
    // Use JUnit test framework
    testCompile 'junit:junit:4.12'
    testCompile project(path:":extjsdk", configuration:"testArtifacts")

    // https://mvnrepository.com/artifact/net.java.dev.jna/jna
    //compile group: 'net.java.dev.jna', name: 'jna', version: '5.6.0'

	implementation  files("${System.env.HIKVISION_LOC}/jna.jar","${System.env.HIKVISION_LOC}/examples.jar")
//	implementation  files("${System.env.HIKVISION_LOC}/examples.jar")
}