import org.gradle.internal.os.OperatingSystem

plugins {
    id 'ru.vyarus.use-python' version '2.3.0'
}

ext {
    if (project.rootProject.hasProperty('pypi_repo')) {
        pypi_repo = project.rootProject.findProperty('pypi_repo')
    } else {
        pypi_repo = 'testpypi'
    }

    if (!project.rootProject.hasProperty('pyExecSrcVersion')) {
        pyExecSrcVersion = '1.1.8'
    }
    vantiqSdkVersion = '1.1.1'
    vantiqConnectorSdkVersion = '1.1.9'
}

python {
    // python binary name (python or python3 by default)
    pythonBinary
    // path to python binary (global by default)
    pythonPath
    // additional environment variables, visible for all python commands
    // Note: We specify the directory paths to search in the pytest.ini file
    environment = [:]

    // minimal required python version (m.m.m)
    minPythonVersion = '3.8'
    // minimal required pip version (m.m.m)
    minPipVersion = '9'

    // show all installed modules versions after pip installation
    showInstalledVersions = true
    // always call module install, even if correct version is already installed
    alwaysInstallModules = false
    // may be used to disable pip cache (--no-cache-dir option)
    usePipCache = true
    // additional pip repositories (--extra-index-url option)
    extraIndexUrls = []
    // trusted hosts for pip install (--trusted-host option)
    trustedHosts = []

    // pip modules installation scope (project local, os user dir, global)
    scope = VIRTUALENV_OR_USER
    // automatically install virtualenv module (if pip modules declared and scope allows)
    installVirtualenv = true
    // if virtualenv not installed (in --user scope), plugin will install exactly this version
    // (known to be working version) to avoid side effects
    virtualenvVersion = '20.4.2'
    // minimal required virtualenv (v20 is recommended, but by default 16 set to not fail previous
    // setups)
    minVirtualenvVersion = '20'
    // used virtualenv path (if virtualenv used, see 'scope')
    envPath
    // copy virtualenv instead of symlink (when created)
    envCopy = false

    pip 'pip:22.0.4'

    // For product runtime
    pip 'websockets:10.2'
    pip 'codetiming:1.3.0'

    // For testing
    pip 'aioresponses:0.7.3'
    pip 'pytest:6.2.5'
    pip 'pytest-asyncio:0.18.3'
    pip 'pytest-pythonpath:0.7.4'
    pip 'pytest-timeout:2.1.0'
    pip 'pytest-html:3.1.1'   // To generate html reports from pytest cases

    // Note:  Placing these last even though they have to do with the product
    // There appears to be an issue with the pytest-asyncio that's present in the test.pypi,org
    // site such that if we try & install from scratch (on my mac), the tests fail with some import
    // problem with pytest-asyncio.  When installed without the test.pypi repo, things work correctly.
    // However, early in the game, we haven't published the SDK's to the public pypi repo (awaiting local
    // review & validation).  By putting these last, we can set up the environment from the general repo.
    // Things fail, but then we can go back and re-enable the test.pypi repo and, without clearing the environment and
    // cache from gradle, things will work as expected.

    // (also for product use)
    pip "vantiqconnectorsdk:${vantiqConnectorSdkVersion}"
    pip "vantiqsdk:${vantiqSdkVersion}"

     // For build generation
    pip 'build:0.7.0'  // For building the distribution
    // For publication
    pip 'twine:4.0.0'
}

tasks.withType(PythonTask) {
    if (rootProject.hasProperty("TestAccessToken")) {
        environment 'VANTIQ_ACCESS_TOKEN', rootProject.findProperty("TestAccessToken") ?: "empty"
    }
    if (rootProject.hasProperty("TestVantiqServer")) {
        environment 'VANTIQ_URL', rootProject.findProperty('TestVantiqServer')
    }
    if (rootProject.hasProperty('TestVantiqUsername')) {
        environment 'VANTIQ_USERNAME', rootProject.findProperty('TestVantiqUsername') ?: 'empty'
    }
    if (rootProject.hasProperty('TestVantiqPassword')) {
        environment 'VANTIQ_PASSWORD', rootProject.findProperty('TestVantiqPassword') ?: 'empty'
    }
}

task pytest(type: PythonTask) {
    println project.projectDir
    module = 'pytest'
}

task generateDockerFile(type: Copy) {
    into 'src/main/docker'
    from 'src/main/templates'
    include '**/Dockerfile.tmpl'
    expand(
            vantiqConnectorSdkVersion: vantiqConnectorSdkVersion,
            vantiqSdkVersion: vantiqSdkVersion,
            pyExecSrcVersion  : pyExecSrcVersion)
    rename ~/(.*)\.tmpl/, '$1'
}

task generateSetupCfg(type: Copy) {
    into project.projectDir
    from 'src/main/templates'
    include '**/setup.cfg.tmpl'
    expand(
            vantiqConnectorSdkVersion: vantiqConnectorSdkVersion,
            vantiqSdkVersion: vantiqSdkVersion,
            pyExecSrcVersion  : pyExecSrcVersion)
    rename ~/(.*)\.tmpl/, '$1'
}

task generateRequirementsTxt(type: Copy) {
    into project.projectDir
    from 'src/main/templates'
    include '**/requirements.txt.tmpl'
    expand(
            vantiqConnectorSdkVersion: vantiqConnectorSdkVersion,
            vantiqSdkVersion: vantiqSdkVersion,
            pyExecSrcVersion  : pyExecSrcVersion)
    rename ~/(.*)\.tmpl/, '$1'
}

// Always regenerate these files since we can't tell when the versions on which they depend have changed.
generateSetupCfg.outputs.upToDateWhen { false }
generateRequirementsTxt.outputs.upToDateWhen { false }
generateDockerFile.outputs.upToDateWhen { false }

task build_distribution(type: PythonTask) {
    String osName = OperatingSystem.current().getName();
    String osVersion = OperatingSystem.current().getVersion();
    println "*** $osName $osVersion was detected."

    if (!OperatingSystem.current().isLinux()) {
        // Consider Linux.

        module = 'build'
    } else {
        command = "-c print('Warning: build_distribution not supported on this platform')"
    }
}

build_distribution.dependsOn generateSetupCfg, generateRequirementsTxt

task verifyRequirements() {
    doLast {
        if (!rootProject.hasProperty('connectorSpecificInclusions')) {
            throw new GradleException("Missing 'connectorSpecificInclusions' property. " +
                "Building the ${project.name} docker image requires that you provide a (possibly empty) " +
                "localRequirements.txt file in the directory specified by the 'connectorSpecificInclusions' property.")
        } else if (!file("${rootProject.getProperty('connectorSpecificInclusions')}/localRequirements.txt").exists()) {
            throw new GradleException("Missing 'localRequirements.txt' file. Building the ${project.name} docker image " +
                " requires that you provide a (possibly empty) localRequirements.txt file in the " +
                " directory specified by the 'connectorSpecificInclusions' property.")
        }
    }
}

if (project.tasks.findByName('buildConnectorImage')) {
    // When all the docker* properties are not present, we may skip constructing this task.
    // In such cases, no dependency is necessary
    buildConnectorImage.dependsOn generateRequirementsTxt, generateSetupCfg, generateDockerFile, verifyRequirements
}

task publish(type: PythonTask) {
    command = "upload --verbose -r ${pypi_repo} dist/*"
    module = 'twine'
}

publish.mustRunAfter build_distribution
publish.dependsOn generateSetupCfg, generateRequirementsTxt, generateDockerFile

// Note: pytest must run AFTER the 'test' task.  That task prepares (i.e. deletes) the directory
// into which the results are to be placed.  Because of that, if pytest runs later, its results
// are deleted.
test.finalizedBy pytest
assemble.finalizedBy build_distribution

task pyclean(type: Delete) {
    delete "${project.projectDir}/dist"
    // The following files are generated from templates so we should remove them on 'clean' action
    delete "${project.projectDir}/requirements.txt"
    delete "${project.projectDir}/setup.cfg"
    delete "${project.projectDir}/src/main/docker/Dockerfile"
}

clean.dependsOn pyclean

// make every other task depend on the generate* tasks.  This way, they run first and the
// gradle dependency analyzer remains happy.
// matching() and all() are "live" so any tasks declared after this line will also depend on generate*
tasks.matching { !it.name.startsWith('generate')
                    && it.name != 'pyclean'
                    && it.name != 'clean' }.all { Task task ->
    task.mustRunAfter generateDockerFile, generateSetupCfg, generateRequirementsTxt
}

// Faux dependencies to keep gradle happy
generateSetupCfg.mustRunAfter generateRequirementsTxt
generateDockerFile.mustRunAfter generateSetupCfg
