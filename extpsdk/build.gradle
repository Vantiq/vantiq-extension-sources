import org.gradle.internal.os.OperatingSystem

plugins {
    id 'ru.vyarus.use-python' version '3.0.0'
}

ext {
    if (project.rootProject.hasProperty('pypi_repo')) {
        pypi_repo = project.rootProject.findProperty('pypi_repo')
    } else {
        pypi_repo = 'testpypi'
    }
    if (!project.rootProject.hasProperty('vantiqConnectorSdkVersion')) {
        vantiqConnectorSdkVersion = '1.3.5'
    }
}

python {
    // python binary name (python or python3 by default)
    pythonBinary
    // path to python binary (global by default)
    pythonPath
    // additional environment variables, visible for all python commands
    // Note: We specify the directory paths to search in the pytest.ini file
    environment = [:]

    // minimal required python version (m.m.m)
    minPythonVersion = '3.10'
    // minimal required pip version (m.m.m)
    minPipVersion = '9'

    // show all installed modules versions after pip installation
    showInstalledVersions = true
    // always call module install, even if correct version is already installed
    alwaysInstallModules = false
    // may be used to disable pip cache (--no-cache-dir option)
    usePipCache = true
    // additional pip repositories (--extra-index-url option)
    extraIndexUrls = []
    // trusted hosts for pip install (--trusted-host option)
    trustedHosts = []

    // pip modules installation scope (project local, os user dir, global)
    scope = VIRTUALENV_OR_USER
    // automatically install virtualenv module (if pip modules declared and scope allows)
    installVirtualenv = true
    // if virtualenv not installed (in --user scope), plugin will install exactly this version
    // (known to be working version) to avoid side effects
    virtualenvVersion = '20.4.2'
    // minimal required virtualenv (v20 is recommended, but by default 16 set to not fail previous
    // setups)
    minVirtualenvVersion = '20'
    // used virtualenv path (if virtualenv used, see 'scope')
    envPath
    // copy virtualenv instead of symlink (when created)
    envCopy = false

    pip 'pip:24.2'
    pip 'pip-tools:7.4.1' // Bootstrap issue -- need this installed for the build
}

task pytest(type: PythonTask) {
    outputs.upToDateWhen { false }
    module = 'pytest'
}

task generateSetupCfg(type: Copy) {
    into project.projectDir
    from 'src/main/templates'
    include '**/setup.cfg.tmpl'
    expand(
        vantiqConnectorSdkVersion: vantiqConnectorSdkVersion)
    rename ~/(.*)\.tmpl/, '$1'
}

tasks.register('generateRequirements', PythonTask) {
    inputs.files('requirements-build.in', 'requirements-sdk.in')
    outputs.file('requirements.txt')
    module = 'piptools'
    command = 'compile requirements-build.in requirements-sdk.in --resolver=backtracking --unsafe-package py ' +
            '--unsafe-package pywin32 -o requirements.txt'
}

// Always regenerate these files since we can't tell when the versions on which they depend have changed.
generateSetupCfg.outputs.upToDateWhen { false }

task build_distribution(type: PythonTask) {
    String osName = OperatingSystem.current().getName();
    String osVersion = OperatingSystem.current().getVersion();
    println "*** $osName $osVersion was detected."

    if (!OperatingSystem.current().isLinux()) {
        // Consider Linux.
        module = 'build'
    } else {
        command = "-c print('Warning: build_distribution not supported on this platform')"
    }
}

build_distribution.dependsOn generateSetupCfg, generateRequirements

task publish(type: PythonTask) {
    command = "upload --verbose -r ${pypi_repo} dist/*"
    module = 'twine'
}

publish.mustRunAfter build_distribution
publish.dependsOn generateSetupCfg, generateRequirements

// Note: pytest must run AFTER the 'test' task.  That task prepares (i.e. deletes) the directory
// into which the results are to be placed.  Because of that, if pytest runs later, its results
// are deleted.
test.finalizedBy pytest
assemble.finalizedBy build_distribution

// delete all the generated files.
task pyclean(type: Delete) {
    delete "${project.projectDir}/dist"
    delete "${project.projectDir}/setup.cfg"
}

clean.dependsOn pyclean

// make every other task depend on the generate* tasks.  This way, they run first and the
// gradle dependency analyzer remains happy.
// matching() and all() are "live" so any tasks declared after this line will also depend on generate*
tasks.matching { !it.name.startsWith('generate')
                    && it.name != 'pyclean'
                    && it.name != 'clean' }.all { Task task ->
    task.mustRunAfter generateSetupCfg
}

if (project.tasks.findByName('buildImages') && project.tasks.findByName('buildConnectorImage')) {
    // This is an SDK so no connector image should be constructed or pushed.
    buildImages.onlyIf { false }
    buildConnectorImage.onlyIf { false }
    pushImages.onlyIf { false }
    pushConnectorImage.onlyIf { false }
}
