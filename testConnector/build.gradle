group 'io.vantiq'
version 'unspecified'

apply plugin: 'java'
apply plugin: 'application'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

mainClassName = 'io.vantiq.extsrc.testConnector.TestConnectorMain'

startScripts{
    doLast{
        def windowsScriptFile = file getWindowsScript()
        def unixScriptFile = file getUnixScript()
        // Add the log Config to the top of the classpath
        windowsScriptFile.text = windowsScriptFile.text.replace("CLASSPATH=", "CLASSPATH=%APP_HOME%\\logConfig;")
        unixScriptFile.text = unixScriptFile.text.replace('CLASSPATH=', 'CLASSPATH=$APP_HOME/logConfig:')
    }
}

// Copies the logger setup into logConfig in the distribution
applicationDistribution.from("src/main/resources") {
    include "log4j2.xml"
    into "logConfig"
}

tasks.withType(Test) {
    if (rootProject.hasProperty("TestConnectorEnvVarName")) {
        systemProperty "TestConnectorEnvVarName", rootProject.findProperty("TestConnectorEnvVarName") ?: "empty"
    }
    if (rootProject.hasProperty("TestConnectorFilename")) {
        systemProperty "TestConnectorFilename", rootProject.findProperty("TestConnectorFilename") ?: "empty"
    }
}

dependencies {
    // This dependency is used internally, and not exposed to consumers on their own compile classpath.
    implementation "com.google.guava:guava:${guavaVersion}"

    implementation project(':extjsdk')

    implementation "org.slf4j:slf4j-api:1.7.25"
    implementation "org.apache.logging.log4j:log4j-slf4j-impl:${log4jVersion}"

    // Use JUnit test framework
    testImplementation 'junit:junit:4.12'
    testImplementation project(path:":extjsdk", configuration:"testArtifacts")
}
