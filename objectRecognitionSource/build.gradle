group 'io.vantiq'
version 'unspecified'

apply plugin: 'java'
apply plugin: 'application'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
    
    // Used for uploading documents (images) to VANTIQ with VANTIQ SDK
    maven {
    	url "https://dl.bintray.com/vantiq/maven"
    }

    maven {
        url "http://vantiqmaven.s3.amazonaws.com/"
    }
}

mainClassName = 'io.vantiq.extsrc.objectRecognition.ObjectRecognitionMain'

// Used to check if OpenCV is needed, and compile with OpenCV or throw errors if needed
// Should be "**/<source file name>.java" for every file that need openCV to run
def opencvDependentSourceFilePatterns = new ArrayList<String>(
    [
        "**/CameraRetriever.java",
        "**/FileRetriever.java",
        "**/NetworkStreamRetriever.java"
    ]
)
def opencvDependentFiles = sourceSets.main.java.matching(
        { delegate.setIncludes(opencvDependentSourceFilePatterns) }
)

// Used to automatically ignore tests for implementations that have been removed.
// The tests should be called "Test<source file name>.java"
// And should be included in the list as "**/Test<source file name>.java"
def standardImplementationTestPatterns = new ArrayList<String>(
    [
        "**/TestCameraRetriever.java",
        "**/TestFileRetriever.java",
        "**/TestYoloProcessor.java",
        "**/TestNetworkStreamRetriever.java",
        "**/TestFtpRetriever.java"
    ]
)

// This is the version of OpenCV that you will be using. Should be "XYZ" for version X.Y.Z

def opencvVersion = "410"

if (System.env.OPENCV_LOC != null ) {
    def ocv = new File(System.env.OPENCV_LOC)
    ocv.eachFile { fname ->
        if (fname.name.startsWith("opencv-") && fname.name.endsWith(".jar")) {
            opencvVersion = fname.name.substring("opencv-".length(), fname.name.lastIndexOf(".jar"))
            logger.warn("Detected OpenCV Version ${opencvVersion}")
        }
    }
} else {
    logger.warn("No OPENCV Located.  Defaulting to OpenCV Version ${opencvVersion}")
}
// Add the logConfig folder to the classpath so that changes to logConfig/log4j2.xml will be used for logging
// and set java.library.path to the contents of the environment variable OPENCV_LOC so the OpenCV native libraries are
// usable 
startScripts{
    doLast{
        def windowsScriptFile = file getWindowsScript()
        def unixScriptFile = file getUnixScript()
        // Add the log Config to the top of the classpath
        windowsScriptFile.text = windowsScriptFile.text.replace("CLASSPATH=", "CLASSPATH=%APP_HOME%\\logConfig;")
        unixScriptFile.text = unixScriptFile.text.replace('CLASSPATH=', 'CLASSPATH=$APP_HOME/logConfig:')
        
        if (!opencvDependentFiles.getFiles().isEmpty()) { // Only set java.library.path for OpenCV if we need it
            // Set the path to the location of the native libraries
            windowsScriptFile.text = windowsScriptFile.text.replace('DEFAULT_JVM_OPTS=',
                    'DEFAULT_JVM_OPTS=-Djava.library.path=%OPENCV_LOC%;%TENSORFLOW_JNI%')
            unixScriptFile.text = unixScriptFile.text.replace('DEFAULT_JVM_OPTS=',
                    'DEFAULT_JVM_OPTS=-Djava.library.path=$OPENCV_LOC:$TENSORFLOW_JNI')
        }
    }
}

// Don't javadoc the Yolo code
javadoc.exclude("edu/*")

// Copies the README and licenses into the distribution
applicationDistribution.from(".") {
    include "README.md"
    include "LICENSE/*"
    into ""
}

// Copies the logger setup into logConfig in the distribution
applicationDistribution.from("src/main/resources") {
    include "log4j2.xml"
    into "logConfig"
}

test {
    // set heap size for the test JVM(s)
    minHeapSize = "2048m"
    maxHeapSize = "2048m"
}

tasks.withType(Test) {
    def os = org.gradle.internal.os.OperatingSystem.current()
    if (os.isWindows()) {
        systemProperty "java.library.path", "${System.env.OPENCV_LOC};${System.env.TENSORFLOW_JNI}"
    } else {
        systemProperty "java.library.path", "${System.env.OPENCV_LOC}:${System.env.TENSORFLOW_JNI}"
    }
    if (rootProject.hasProperty("TestAuthToken")) {
        systemProperty "TestAuthToken", rootProject.findProperty("TestAuthToken") ?: "empty"
    }
    if (rootProject.hasProperty("TestVantiqServer")) {
        systemProperty "TestVantiqServer", rootProject.findProperty("TestVantiqServer") ?: "empty"
    }
    // Use the build dir as a base to get our various test artifacts.
    systemProperty "buildDir", "${buildDir}"
}

// Logs a warning if the .dll/.so/.dylib cannot be found for OpenCV when OpenCV is needed
assemble.doLast {
    def os = org.gradle.internal.os.OperatingSystem.current()
    // Only need to warn about missing libraries if there are files dependent on OpenCV
    if (!opencvDependentFiles.getFiles().isEmpty()) {
        if (os.isWindows()) {
            if (!file("${System.env.OPENCV_LOC}\\opencv_java${opencvVersion}.dll").exists()) {
                logger.warn("Cannot find opencv_java${opencvVersion}.dll in '${System.env.OPENCV_LOC}'. Ensure that the requested "
                    + "file exists at the folder specified in the environment variable OPENCV_LOC")
            }
        } else if (os.isLinux()) {
            if (!file("${System.env.OPENCV_LOC}/libopencv_java${opencvVersion}.so").exists()) {
                logger.warn("Cannot find libopencv_java${opencvVersion}.so in '${System.env.OPENCV_LOC}'. Ensure that the requested "
                    + "file exists at the folder specified in the environment variable OPENCV_LOC")
            }
        } else if (os.isMacOsX()) {
            if (!file("${System.env.OPENCV_LOC}/libopencv_java${opencvVersion}.dylib").exists()) {
                logger.warn("Cannot find libopencv_java${opencvVersion}.dylib in '${System.env.OPENCV_LOC}/'. Ensure that the "
                    + "requested file exists at the folder specified in the environment variable OPENCV_LOC")
            }
        } else {
            logger.warn("Compiling unknown file system. Please ensure that the compiled OpenCV C++ library is inside " 
                + "'${System.env.OPENCV_LOC}' so that the program can function correctly")
        }
    }
}

compileJava.doFirst {
    removeUnnecessaryTests()
}

// Make 'removeUnnecessaryTests' a callable function
// It makes the build not compile tests for any of the standard image retriever or neural net implementations that
// have been deleted 
ext.removeUnnecessaryTests = {

    standardImplementationTestPatterns.removeIf {
        testPattern -> 
            def testedFile = testPattern.substring(7) // Remove '**/Test' from the Pattern, leaving the tested file
            def testedFilePattern = '**/' + testedFile // Add '**/' to the tested file so it can be found in the source sets
            // Get all files that match "**/<source file>" which should just be the source file or nothing if the
            // source file has been removed
            def standardFile = sourceSets.main.java.matching(
                    { delegate.include(testedFilePattern) }
            )
            return !standardFile.getFiles().isEmpty() // Return true (and remove) if the file it tests still exists
    }
    // Now standardImplementationTestPatterns only contains tests for files that don't exist
    
    // Remove all tests for files that don't exist
    sourceSets.test.java.filter.exclude(standardImplementationTestPatterns)
}


configurations {
    oldYoloNames
    metaTest
}

ext {
    currentCocoVersion = 1.2
}

dependencies {
    compile project(':extjsdk')
    
    compile "org.slf4j:slf4j-api:1.7.25"
    compile "org.apache.logging.log4j:log4j-slf4j-impl:2.11.0"
    

    compile "com.fasterxml.jackson.core:jackson-databind:2.9.3"
    
    // Used by YoloProcessor alone. These two dependencies and all files in the edu.* package may be removed if YoloProcessor
    // is removed
    compile "org.tensorflow:tensorflow:1.11.0"
    compile "org.apache.commons:commons-math3:3.6.1"
    
    // Used by FtpRetriever alone. Can be removed if FtpRetriever is removed
    compile "commons-net:commons-net:3.6"
    compile "com.jcraft:jsch:0.1.54"
    
    // The following dependencies are used to download neural net model information (and test artifacts) used in our
    // tests and by some customer applications.  Formerly, some of these were downloaded from the following locations,
    //    def sampleVideoUrl = new URL("https://www.dropbox.com/s/h5yd00qr8sfgdlt/sampleVideo.mov?dl=1")
    //    def yoloGraphUrl = new URL("https://www.dropbox.com/s/lhnygcd6o6275jb/yolo.pb?dl=1")
    // but we have now standardized on this mechanism.  The endpoints immediately above are left here for a short
    // period of time in the event they prove useful to someone in the short term.  They are, however, temporary,
    // the data there may not be available for an extended period of time.


    testRuntime "vantiq.testArtifacts:sampleVideo:1.0@mov"
    testRuntime "vantiq.models:coco:${currentCocoVersion}@meta"
    testRuntime "vantiq.models:coco:${currentCocoVersion}@names"
    testRuntime "vantiq.models:coco:${currentCocoVersion}@pb"

    oldYoloNames "vantiq.models:coco:1.0@pb"
    oldYoloNames "vantiq.models:coco:1.0@names"
    oldYoloNames "vantiq.models:yolo:1.0@pb"
    
    metaTest "vantiq.models:coco:1.1@meta"
    metaTest "vantiq.models:coco:1.1@pb"


    // This imports OpenCV if it is needed, and throws an error if OpenCV is needed but not properly setup
    // The opencv
    if (!opencvDependentFiles.getFiles().isEmpty()) { // Only look for OpenCV if we need it
        if (System.env.OPENCV_LOC == null ) { // Fail out if OpenCV is not available 
            // Setting depFileNames to the files still dependent on OpenCV
            def depFileNames = "'"
            opencvDependentFiles.getFiles().forEach {file -> depFileNames += file.getName() + "', '"}
            depFileNames = depFileNames.substring(0, depFileNames.length() - 3) // Remove the last ", '"
            throw new Exception("Environment variable 'OPENCV_LOC' is not set. Either set it to a location containing " + 
            "opencv-${opencvVersion}.jar and the compiled OpenCV library, or remove the following files: ${depFileNames}")
        } else if (!file("${System.env.OPENCV_LOC}/opencv-${opencvVersion}.jar").exists()) { // Fail out if the OpenCV jar is missing
            // Setting depFileNames to the files still dependent on OpenCV
            def depFileNames = "'"
            opencvDependentFiles.getFiles().forEach {file -> depFileNames += file.getName() + "', '"}
            depFileNames = depFileNames.substring(0, depFileNames.length() - 3) // Remove the last ", '"
            throw new Exception("Could not find opencv-${opencvVersion}.jar in '${System.env.OPENCV_LOC}'. Either add the jar to the "
                + "folder specified in the environment variable OPENCV_LOC or remove the following files: ${depFileNames}")
        } else {
            compile files("${System.env.OPENCV_LOC}/opencv-${opencvVersion}.jar")
        }
    }
    
    // Used for uploading documents (images) to VANTIQ with VANTIQ SDK
    compile 'io.vantiq:vantiq-sdk:1.0.24'

    // Use JUnit test framework
    testCompile 'junit:junit:4.12'
    testCompile project(path:":extjsdk", configuration:"testArtifacts")
}

task copyModels(type: Copy, dependsOn: [configurations.testRuntime, configurations.metaTest]) {
    from configurations.testRuntime.find { it.name == "coco-${currentCocoVersion}.pb" },
        configurations.testRuntime.find { it.name == "coco-${currentCocoVersion}.names" },
        configurations.testRuntime.find { it.name == "coco-${currentCocoVersion}.meta" },
        
        // Used for testing different height/width from .meta file
        configurations.metaTest.find { it.name == "coco-1.1.meta" },
        configurations.metaTest.find { it.name == "coco-1.1.pb" }

    into "$buildDir/models/"
}

task copyOldCocoModels(type: Copy, dependsOn: [configurations.testRuntime, configurations.oldYoloNames]) {
    from configurations.oldYoloNames.find { it.name == 'coco-1.0.pb' },
        configurations.oldYoloNames.find { it.name == 'coco-1.0.names' }
        

    into "$buildDir/models/"
}


task copyOldYoloModels(type: Copy, dependsOn: [configurations.testRuntime, configurations.oldYoloNames]) {
    from configurations.oldYoloNames.find { it.name == 'yolo-1.0.pb' },
        configurations.oldYoloNames.find { it.name == 'coco-1.0.names' }

    rename { filename ->
        if (filename.contains("-1.0")) {
            return filename - "-1.0" // Remove version number as it was not present in the older model
        } else {
            return null // no change
        }
    }

    into "$buildDir/models/"
}

task copyTestResources(type: Copy, dependsOn: [configurations.testRuntime, configurations.oldYoloNames]) {
    from configurations.testRuntime.find { it.name == 'sampleVideo-1.0.mov' },
        sourceSets.test.resources.asList()

    into "$buildDir/testResources/"
}

test.dependsOn copyModels, copyTestResources
